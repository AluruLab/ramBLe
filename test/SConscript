##
# @file SConscript
# @brief Provides functionality for building the test suite.
import os

Import('env')

gTestLibs = [
             'gtest',
             ]

srcFiles = [
            'test.cpp',
            ]

built = False
# Do not modify the provided build environment
buildEnv = env.Clone()
# Check if the header and the library files can be located
conf = Configure(buildEnv)
if not conf.CheckCXXHeader('gtest/gtest.h'):
  Return('built')
for lib in gTestLibs:
  if lib in buildEnv.get('LIBS', []):
    continue
  if not conf.CheckLib(lib, language='C++'):
    Return('built')
buildEnv = conf.Finish()

buildEnv.Program(target=buildEnv.testName, source=srcFiles)

buildEnv.Install(buildEnv.testDir, buildEnv.testName)
built = True
Return('built')
